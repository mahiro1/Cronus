//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//- Funcionários do Aeroporto de Einbroch                     -
//-------------------------------------------------------------
//- Por:                                                      -
//- L0ne_W0lf, Muad_Dib                                       -
//-------------------------------------------------------------
//- Compatível Com:                                           -
//- Cronus                                                    -
//-------------------------------------------------------------
//- Descrição:                                                -
//- Funcionários do Aeroporto de Einbroch                     -
//-------------------------------------------------------------
//- Comentários Adicionais:                                   -
//- Official NPCs.                                            -
//- Traduzido para pt-BR. [Evanio]                            -
//- Revisado de acordo com o bRO. [Portalcake]                -
//-------------------------------------------------------------

airport,143,43,5	script	Assistente do Aeroporto#airport1a::airport1	4_F_01,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao aeroporto. Em que posso ajudar?";
	next;
	if (select("Entrar no Aeroporto.:Cancelar.") == 1) {
		mes "[Assistente do Aeroporto]";
		mes "A taxa de entrada é de 1.200 Zenys.";
		mes "Se tiver um bilhete para o Aeroplano, pode entrar de graça.";
		mes "Gostaria de entrar?";
		next;
		if (select("Sim:Não") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1;
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				Zeny -= 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Assistente do Aeroporto]";
			mes "Desculpe, mas você não tem dinheiro suficiente.";
			mes "A taxa de entrada é de 1.200 Zenys.";
			mes "Obrigado.";
			close;
		}
	}
	mes "[Assistente do Aeroporto]";
	mes "Obrigado.";
	close;
}

airport,158,43,5	duplicate(airport1)	Assistente do Aeroporto#airport1b	4_F_01
airport,126,43,5	duplicate(airport1)	Assistente do Aeroporto#airport1c	4_F_01

airport,143,49,3	script	Assistente do Aeroporto#airport2a::airport2	4_F_01,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao aeroporto. Em que posso ajudar?";
	next;
	if (select("Sair do Aeroporto.:Cancelar.") == 1) {
		mes "[Assistente do Aeroporto]";
		mes "Por favor, lembre-se de que terá de pagar a taxa de entrada para entrar no aeroporto novamente.";
		mes "Quer sair agora?";
		next;
		if (select("Sim:Não") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Assistente do Aeroporto]";
	mes "Volte sempre. Obrigado.";
	close;
}

airport,126,51,3	duplicate(airport2)	Assistente do Aeroporto#airport2b	4_F_01
airport,158,50,3	duplicate(airport2)	Assistente do Aeroporto#airport2c	4_F_01

einbroch,94,267,3	script	Assistente do Aeroplano#ein01	4_F_02,{
	mes "[Assistente do Aeroplano]";
	mes "Bem-vindo ao Aeroporto de Einbroch";
	mes "Por favor, use essa porta para Embarcar no Aeroplano com escalas";
	mes "em Lighthalzen, Juno e Hugel";
	next;
	mes "[Assistente do Aeroplano]";
	mes "Ou, se Einbroch for seu destino final, por favor, desça as escadas";
	mes "e pergunte ao Assistente de Desembarque o caminho para o";
	mes "terminal principal. Obrigada";
	mes "e aproveite a viagem.";
	close;
}
