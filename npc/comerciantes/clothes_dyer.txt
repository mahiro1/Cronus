//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------

//= Tinturas de roupa
//===== Por: ==================================================
//= Usnul
//===== Versão Atual: =====================================
//= 2.6
//===== Compatível Com: ===================================
//= Cronus
//===== Descrição: =========================================
//= Tinturas de roupas padrão.
//===== Additional Comments: =================================
//= Fully working
//= 2.1 Shortened some labels to make this script loading,
//= Added Black+White Colors desc [Lupus]
//= 2.1a added adv classes + baby class support [Lupus]
//= 2.2 Spell checked. [Nexon]
//= 2.3 Fixed dis $hit! [Poki#3]
//= 2.4 Disabled cloth dyeing since it's not implemented
//	   on official servers and cause your client to crash
//	   [Playtester]
//= 2.5 Optimized. [Euphy]
//= 2.6 Aplicada Tradução(PT_BR). [SkyHD]
//============================================================

prt_in,284,168,2	script	Dyer Ginedin Rephere	1_M_JOBTESTER,{
function Dyes;

	set .@EnableDyes,1;	// Disable (set to 0) se palletas causar erros [see note v2.4]

	set .n$,"[Dyer Ginedin Rephere]";
	mes .n$;
	mes "11... 12... Mmm... Bom. Acho que vou ser capaz de terminar antes da festa de hoje à noite. Oh! Eu não percebi que você estava aqui!";
	while(1) {
		next;
		mes .n$;
		mes "Como posso ajudá-lo?";
		next;
		switch(select("- Conversar:- tingir roupa:- lista de preços:- ^777777Cancelar^000000")) {
		case 1:
			mes .n$;
			mes "A vida pode ter começado um pouco melhor, mas quando eu olho para todos os pedidos que recebi ... Ufa!";
			mes "Parece que as jovens de Rune-Midgard deve estar muito bem de vida nos dias de hoje.";
			next;
			mes .n$;
			mes "Não é que eu acho que eles estão entregando-se a qualquer coisa de luxo, você mente.";
			mes "Eu não acho que haja nada de errado com a busca da beleza e estar na moda.";
			next;
			mes .n$;
			mes "Para nós, seres humanos, que não têm pele colorida ou escalas de decoração, roupas são um dos poucos meios que temos de mostrar a nossa personalidade, estilo e beleza.";
			next;
			mes .n$;
			mes "Haha ... Quando eu penso sobre isso, eu realmente sinto que o meu trabalho vale a pena eu acredito que eu fornecer um serviço que as pessoas desejam.".;
			next;
			mes .n$;
			mes "Você pode sentir isso também? energia que é liberada quando tecido áspero e couro são trazidos à vida com mais cor .....?";
			next;
			mes .n$;
			mes "Embora o processo é muito tedioso e demorado, a alegria e a felicidade que eu sinto quando um vestido é terminado,isso tudo vale a compensa de todo o trabalho duro!";
			break;
		case 2:
			mes .n$;
			if (!.@EnableDyes) {
				mes "Desculpe, mas eu estou muito ocupado com outros trabalhos para tingir suas roupas.";
				break;
			}
			mes "Oh... você precisa do meu trabalho? bem... ok, bem.";
			next;
			mes .n$;
			mes "Por favor, escolha uma cor";
			mes "que combina com você.";
			next;

			// Dyes(<color value>,<dye number>{,...});
			// Colors: [1]Orange, [2]Violet, [3]Red, [4]Black, [5]Green, [6]Blue, [7]White, [8]Yellow
			switch(BaseJob) {
			case Job_Novice:
				if (Sex) Dyes(4,1,6,2,5,3); else Dyes(6,1,3,2,5,3,4,4);
			case Job_Swordman:
				if (Sex) Dyes(4,1,6,2,5,3); else Dyes(3,1,5,3,4,4);
			case Job_Mage:
				if (Sex) Dyes(3,1,2,2,1,3,7,4); else Dyes(3,1,2,2,7,3,4,4);
			case Job_Archer:
				if (Sex) Dyes(4,1,2,2,5,3); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Acolyte:
				if (Sex) Dyes(6,1,3,2,7,3,4,4); else Dyes(3,1,5,3,4,4);
			case Job_Merchant:
				if (Sex) Dyes(4,1,6,2,5,3); else Dyes(2,1,5,3,4,4);
			case Job_Thief:
				if (Sex) Dyes(5,1,3,2,7,4); else Dyes(3,1,7,3,4,4);
			case Job_Knight:
			case Job_Crusader:
				if (Sex) Dyes(2,1,3,3,4,4); else Dyes(6,1,7,3,4,4);
			case Job_Priest:
			case Job_Monk:
				if (Sex) Dyes(6,1,5,2,3,3,7,4); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Wizard:
			case Job_Sage:
				if (Sex) Dyes(6,1,5,3,4,4); else Dyes(3,1,6,2,7,3,5,4);
			case Job_Blacksmith:
			case Job_Alchemist:
				if (Sex) Dyes(4,4,5,2,7,3); else Dyes(3,1,5,2,2,3,4,4);
			case Job_Hunter:
			case Job_Bard:
			case Job_Dancer:
				if (Sex) Dyes(4,1,6,2,5,3); else Dyes(6,1,5,3,2,4);
			case Job_Assassin:
			case Job_Rogue:
				if (Sex) Dyes(6,1); else Dyes(4,1,8,2,7,3);
			case Job_SuperNovice:
				if (Sex) Dyes(2,1,6,2,5,3,4,4); else Dyes(6,1,3,2,5,3,4,4);
			default:
				mes "Uau, eu nunca vi roupas como essa antes! Me desculpe, mas eu não acho que eu posso pintá-lo.";
				close;
			}
		case 3:
			mes .n$;
			mes "Cada cor custa 10000z e uma tintura:";
			mes " ";
			mes " - ^FF4422Vermelho^000000: tintura Vermelho";
			mes " - ^D5A500Amarelo^000000: tintura Limão";
			mes " - ^AA00AARoxo^000000: tintura Roxo";
			mes " - ^FF8800Laranja^000000: tintura Laranja";
			mes " - ^4422FFAzul^000000: tintura AzulCobalto";
			mes " - ^009500Verde^000000: tintura VerdeEscuro";
			mes " - Preto: tintura Preto";
			mes " - Branco: tintura Branco";
			break;
		case 4:
			goto L_End;
		}
	}

L_End:
	mes .n$;
	mes "Sinta-se em casa, mesmo que eu não tenho tempo para você.";
	close;

function Dyes {
	setarray .@DyeName$[0],"a cor padrão","laranja","Roxo","Vermelho","Preto","Verde","Azul","Branco","Amarelo";
	setarray .@DyeItems[0],0,980,981,975,983,979,978,982,976;
	set .@menu$,"- Default:";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+2)
		set .@menu$, .@menu$+"- "+.@DyeName$[getarg(.@i)]+":";
	set .@menu$, .@menu$+"- ^777777Cancel^000000";
	set .@s, select(.@menu$)-1;
	if (.@s == (getargcount()/2)+1) goto L_End;
	mes .n$;
	if (.@s == 0) {
		mes "Eu posso mudar sua tintura para";
		mes "o padrão de graça.";
		mes "Você tem certeza?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			mes .n$;
			setlook 7,0;
			mes "Suas roupas foram tingidas "+.@DyeName$[0]+".";
			close;
		case 2:
			goto L_End;
		}
	}
	if (!countitem(.@DyeItems[getarg((.@s-1)*2)])) {
		mes "Para tingir suas roupas, eu vou precisar das tinturas apropriadas. Por favor, verifique a minha lista de preços para as informações.";
		close;
	}
	if (Zeny < 10000) {
		mes "Sinto muito, mas você não tem dinheiro suficiente.";
		close;
	}
	delitem .@DyeItems[getarg((.@s-1)*2)],1;
	Zeny -= 10000;
	setlook 7, ((.@s)?(getarg((.@s-1)*2+1)):0);
	mes "Suas roupas foram tingidas"+((.@s)?.@DyeName$[getarg((.@s-1)*2)]:.@DyeName$[0])+".";
	close;
}
}
