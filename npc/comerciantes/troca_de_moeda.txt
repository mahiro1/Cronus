//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------
//= Comercio de moedas
//===== Por: ==================================================
//= rAthena Dev Team
//===== Versão Atual: =====================================
//= 1.3
//===== Compatível com: =========================================
//= Cronus
//===== Descrição: =========================================
//= [Official Conversion]
//= Coin redemption NPCs.
//===== Comentários Adicional: =================================
//= 1.0 First version
//= 1.1 Fixed checkweight. [Gepard]
//= 1.2 Optimized and merged into one file. [Euphy]
//= 1.3 Aplicada Tradução(PT_BR). [SkyHD]
//============================================================

// Manuk & Splendide
//============================================================
-	script	::merchant_13_2	-1,{
	set .@n$, "["+strnpcinfo(1)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(1201,1)) {
			mes "Parece que você está carregando muitas coisas."; 
			mes "Por que não colocar alguns de seus itens no armazenamento e voltar?";
			close;
		}
		mes "Olá.";
		mes "O que você está procurando?";
		next;
		if (strnpcinfo(1) == "Mercador de Manuk") {
			setarray .@items[2],12342,12343,12348;
			set .@Book,11019; //Manuk_Selling_Item
			set .@coin,6080; //Manuk_Coin
			set .@i, select("Ver descrição de item: Oportunidade de Manuk: Coragem de Manuk: Fé de Manuk: Cancelar");
		}
		else {
			setarray .@items[2],12344,12345,12349;
			set .@Book,11018; //Splendide_Selling_Item
			set .@coin,6081; //Splendide_Coin
			set .@i, select("Ver descrição de item:Comprar Pinguicula's Fruit Jam:Comprar Luciola's Honey Jam:Comprar Cornus' Tears:Não fazer nada");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "Aqui estão as descrições dos itens.";
			close2;
			readbook .@Book,1;
			end;
		}
		else if (.@i < 5) {
			mes "Eu posso vendê-lo 3 "+getitemname(.@items[.@i])+" for ^3131FF1 moeda^000000.";
			next;
			switch(select("Comprar:Não comprar")) {
			case 1:
				if (countitem(.@coin)) {
					mes .@n$;
					mes "Obrigado por vir.";
					delitem .@coin,1;
					getitem .@items[.@i],3;
					close;
				}
				else {
					mes .@n$;
					mes "Você não tem moedas suficientes.";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "Venha novamente, se você mudar de idéia.";
		close;
	}
	else {
		if (strnpcinfo(1) == "Mercador de Manuk") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
man_in01,286,16,1	duplicate(merchant_13_2)	Mercador de Manuk	4_MAN_PIOM
spl_in01,110,326,5	duplicate(merchant_13_2)	Mercador de Splendide	4_F_FAIRYKID4
