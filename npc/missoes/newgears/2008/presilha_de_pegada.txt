//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-  Presilha de Pegada (Equipamentos para Cabeça 2006)       -
//-------------------------------------------------------------
//- Por:                                                      -
//-  (*)Athena Dev Team                                       -
//-------------------------------------------------------------
//- Versão: 1.0b                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-   Missão para adquirir o equipamento Presilha de Pegada.  -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//- 	1.0b - Realizado tradução do script. [?]              -
//-------------------------------------------------------------

gef_fild05,80,149,3	script	Myu#08_hat	4_F_YUNYANG,{
	mes "[Myu]";
	mes "Meow...";
	emotion e_omg;
	next;
	if(hatcat2008 == 0) {
		mes "[Myu]";
		mes "Oh? Aren't you an adventurer?";
		mes "What brings you here? Ho, you are not here to harm Wild Roses, are you?";
		mes "(Meow!)";
		next;
		select("Meow..?");
		mes "[Myu]";
		mes "Ah, Never mind that.";
		mes "This place is the village of Deserted people.";
		mes "Homeland of those who walk in the shadows and lay low.";
		next;
		mes "[Myu]";
		mes "....And yet, our peace is being disturbed by those barking dogs.";
		mes "That's right, Adventurer.";
		mes "Would you be so kind and punish those Kobold Archers?";
		next;
		mes "[Myu]";
		mes "They have their own territory south of here, but they're still trying to invade ours.";
		next;
		mes "[Myu]";
		mes "How Impudent. I tried to talk to them in a peaceful way, or even fight them off, but it was of no use.";
		next;
		mes "[Myu]";
		mes "So what I'm asking you is... to drive them out of our territory by using all means necessary..";
		next;
		mes "[Myu]";
		mes "If you manage to do that, I will give you something precious which I really adore.";
		next;
		switch(select("That sounds troublesome..:What should I do?")){
		case 1:
			mes "[Myu]";
			mes "Hmph, yeah, it was kinda funny huh.";
			mes "Anybody who comes here....they all come to harm the Wild Roses..";
			mes "I failed to notice that.";
			next;
			mes "[Myu]";
			mes "...It was a fruitless effort Meow...";
			next;
			mes "["+strcharinfo(0)+"]";
			mes " (Meow again...What a weirdo.)";
			close;
		case 2:
			mes "[Myu]";
			mes "Of course take care of those Kobold Archers Meow!";
			mes " ";
			mes "(Meow!)";
			next;
			select("Meow again?!");
			mes "[Myu]";
			mes "Hmm Hmm I told you to never mind that.";
			mes "So as I was saying...";
			mes "I mean... meow... er...";
			next;
			mes "[Myu]";
			mes "Exactly 1,000 times no more, no less.";
			mes "Give them 1,000 warnings and they will back off.";
			next;
			select("What do you mean by 1,000 warnings...?");
			mes "[Myu]";
			mes "Hu~Do I have to spell it out for you?";
			next;
			mes "As she says that, Myu slides her neck mocking like she's slitting her throat.";
			next;
			switch(select("Isn't that too much?:That's easy")){
			case 1:
				mes "[Myu]";
				mes "..But they won't give up if we dont really teach them a lesson.";
				mes "Well If you think it's too much for you, so be it.";
				close;
			case 2:
				mes "[Myu]";
				mes "But you have to keep one thing in mind and it is";
				mes "obiously, NEVER EVER do any harm to Wild Roses.";
				next;
				mes "[Myu]";
				mes "If you do, it's all over for you.";
				mes "So you better be careful.. ";
				mes "Meow Meow Meow..";
				set hatcat2008,1;
				setquest 7054;
				setquest 7055;
				close;
			}
		}
	} else if(hatcat2008 == 1) {
		if(checkquest(7055,HUNTING) == 2) {
			mes "[Myu]";
			mes "Did you think I didn't know what you have done?";
			mes "Huh?";
			next;
			mes "[Myu]";
			mes "How..just how could you kill those precious children..?";
			mes "And you were saying that you are on our side?!";
			next;
			mes "[Myu]";
			mes "Our business is over!!!";
			mes " ";
			mes "(Meow..)";
			set hatcat2008,0;
			erasequest 7054;
			erasequest 7055;
			close;
		} else if(checkquest(7054,HUNTING) == 2) {
			mes "[Myu]";
			mes "Oh..wow unbelievable!";
			mes "Now, those Kobolds should have learned a lesson, haven't they?";
			mes "Meow Meow Meow!";
			next;
			mes "[Myu]";
			mes "Here, Take this.";
			mes "This is the Seal of our 'Brave Kitty Cats'.";
			mes "This is soooo valuable, so don't lose it.";
			next;
			select("This is it?");
			mes "[Myu]";
			mes "What more did you expect?";
			mes "That Pow is really a valuable thing!";
			mes "Now, just take it and leave. Wild Roses feel uncomfortable with an adventurer around them.";
			set hatcat2008,2;
			getitem 5446,1;
			erasequest 7054;
			erasequest 7055;
			close;
		}
		mes "[Myu]";
		mes "You get it now, huh?";
		mes "ONE THOUSAND TIMES! -chuckles-";
		mes "(Meow Meow Meow~)";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "(What a weirdo.. Anyways..what's with that meow meow sound?)";
		mes "Just keep your promises.";
		mes "after the work is done...Ok?";
		next;
		mes "[Myu]";
		mes "Of course! Don't worry about that~";
		close;
	} else if(hatcat2008 == 2) {
		mes "[Myu]";
		mes "This place is the village of Deserted people.";
		mes "Homeland of those who walk in the shadows and lay low.";
		next;
		mes "[Myu]";
		mes "Oh, Aren't you the Adventurer who took the Seal of our 'Brave Kitty Cats'.";
		mes "What brings you here?";
		next;
		mes "[Myu]";
		mes "Oh yeah, right, even after all the threats we gave the Kobolds..";
		mes "our situation hasn't changed at all. Would you mind helping us once more?";
		next;
		mes "Myu slides her neck mocking like she's slitting her throat.";
		mes "Her mouth looks like it's saying.";
		mes " 'O.N.E. T.H.O.U.S.A.N.D.' ";
		next;
		switch(select("NO!:Sure.")) {
		case 1:
			mes "[Myu]";
			mes "...!...";
			mes ".. Ah.. that's too bad.";
			mes "Well, looks like a bait has spoiled.... ";
			mes "It's ok. That's how it is. Meow.";
			close;
		case 2:
			mes "[Myu]";
			mes "You know the drill right?";
			mes "Never ever touch the Wild Roses, Only hunt down the Kobold Archers.";
			mes "Give them 1,000 times despair!";
			set hatcat2008,1;
			setquest 7054;
			setquest 7055;
			close;
		}
	}
	mes "[Myu]";
	mes "Meow..";
	mes "This Sunshine...makes me sleepy..";
	close;
}