//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-  Presilha de Tulipa (Equipamentos para Cabeça 2004)       -
//-------------------------------------------------------------
//- Por:                                                      -
//-  (*)Athena Dev Team                                       -
//-------------------------------------------------------------
//- Versão: 1.6c                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-   Missão para adquirir o equipamento Presilha de Tulipa.  -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//- 	1.6c - Realizado tradução do script. [?]              -
//-------------------------------------------------------------

geffen,83,189,5	script	Seth#1	4_M_KID2,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes " [Seth]";
		mes "Whoa, why are you carrying so many items with you?";
		mes "You look so heavy...! Maybe you can walk more easily if you put some of your stuff away?";
		close;
	}
	mes " [Seth]";
	mes "Hello, my name is Seth!";
	mes "I like folding paper and making origami. I made my mommy a big paper boat, and she gave me a big smile and patted my head!";
	emotion e_heh;
	next;
	mes "[Seth]";
	mes "Today at school, I learned how to make a paper flower! The flower was sort of hard, but now I can do it! Then I made a tiny tiny flower and put in on my head~";
	emotion e_lv;
	next;
	if (countitem(2278) > 0 && countitem(975) > 0) {
		mes "[Seth]";
		mes "Hey~!";
		mes "You have a Mr. Smile and Scarlet Dyestuffs! Yaaay~! I could use those and make you a paper flower. C'mon, lemme show you!";
		emotion e_swt2;
		next;
		mes "[Seth]";
		mes "Lemme make";
		mes "one for you,";
		mes "pwease pwease?";
		emotion e_swt2;
		next;
		switch(select("Alright!:No thanks kid.:Boys aren't supposed to make flowers.")) {
		case 1:
			emotion e_heh;
			mes "[Seth]";
			mes "Yay~";
			mes "You're the best!";
			next;
			mes "[Seth]";
			mes "Let's see...";
			mes "I cut Mr. Smile this way~.";
			mes "*Snip Snip Snip~~*";
			mes "*Snip...Snip...*";
			mes "Yyyyyaaaeeep...";
			emotion e_ho;
			next;
			mes "[Seth]";
			mes "Tah dah~!";
			next;
			delitem 2278,1; //Mr_Smile
			delitem 975,1; //Scarlet_Dyestuffs
			getitem 5077,1; //Tulip_Hairpin
			mes "[Seth]";
			mes "Heheheh~!";
			mes "I'm done!";
			mes "I did a good job, didn't I?";
			next;
			if (BaseJob == Job_Novice || BaseJob == Job_SuperNovice) {
				mes "[Seth]";
				mes "Oh, here's the leftovers. Mommy says that we should always save things. Heh heh~";
				getitem 935,1; //Shell
				next;
			}
			mes "[Seth]";
			mes "I'm a good boy!";
			mes "I'm gonna make one for my little sister~! And everybody I love!";
			next;
			mes "[Seth]";
			mes "If you want to see me make another one, just bring more Scarlet Dyestuffs and Mr.Smile, okay? Bye bye~!";
			close;
		case 2:
			mes "[Seth]";
			mes "*Sniff sniff*";
			mes "O-okay...";
			mes "But I just wanted to show you how good I am...";
			emotion e_sob;
			close;
		case 3:
			mes "[" + strcharinfo(0) + "]";
			mes "Hey.";
			next;
			mes "[" + strcharinfo(0) + "]";
			mes "Boys aren't supposed to make flowers.";
			next;
			mes "[Seth]";
			mes "Waaaaaaah~!";
			mes "But flowers are so nice and pweety!!!";
			emotion e_sob;
			close;
		}
	}
	else {
		mes "[Seth]";
		mes "You want me to show you? Huh?";
		mes "I can do it you want!";
		next;
		mes "[Seth]";
		mes "Uh oh...";
		mes "But I don't have anything to make paper flowers out of.";
		next;
		if (select("I'll get what you need.:Too bad!") == 1) {
			mes "[Seth]";
			mes "Okay okay...";
			mes "I neeeeed...";
			mes "^3131FF1 Mr. Smile^000000 and";
			mes "^3131FF1 Scarlet Dyestuffs^000000.";
			mes "Then I can make you a tiiiiny little flower.";
			next;
			mes "[Seth]";
			mes "Seth will wait for you here. Promise me you'll come back?";
			close;
		}
		emotion e_sob;
		mes "[Seth]";
		mes "Wah~~~~~!";
		close;
	}
}