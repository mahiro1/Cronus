//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-  Presilhas (Equipamentos para Cabeça 2004)                -
//-------------------------------------------------------------
//- Por:                                                      -
//-  (*)Athena Dev Team                                       -
//-------------------------------------------------------------
//- Versão: 1.6c                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-   Missão para adquirir o equipamentos Presilha de X,      -
//-   Curativo Gigante e Presilha de Flor.                    -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//- 	1.6c - Realizado tradução do script. [?]              -
//-------------------------------------------------------------

geffen,129,148,3	script	Argen#1	4_M_YOYOROGUE,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "[Argen]";
		mes "Hey~ why are you carrying so many items?";
		mes "You mind sharing them with me?";
		close;
	}
	emotion e_hmm;
	mes "[Argen]";
	mes "Man, oh man~";
	mes "Am I bored~!";
	mes "If I only knew more tricks with this yo-yo. Then I could really impress the ladies.";
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "Impress the ladies...?";
	next;
	emotion e_gasp;
	if (Sex) {
		mes "[Argen]";
		mes "Yeah dude. There are two things chicks dig. Small, adorable presents. And suave dudes.";
		next;
		mes "[Argen]";
		mes "So, I got the cutesy present junk down. You know, instead of like, spending money on like a real gift, I make these little hairpin thingees.";
		next;
		mes "[Argen]";
		mes "As for being suave? Well, I do have this yo-yo...";
		next;
		mes "[Argen]";
		mes "Heh. You look like you don't like wasting cash on girls either. I'm down with that. If you like, I can make you a hairpin that you can give as gifts to the ^FF3333ladies^000000.";
		next;
		mes "[Argen]";
		mes "All you gotta do is bring me the stuff to make it. And if that hairpin's for yourself, well, I'm down with that jive too.";
	}
	else {
		mes "[Argen]";
		mes "Whoa~! A lady...";
		mes "Wow, you're really cute! Tell you what...";
		next;
		mes "[Argen]";
		mes "Since you're such a cutie, I'll put together a nice little hairpin for you. All you gotta do is bring me the stuff to make it.";
		next;
		mes "[Argen]";
		mes "So go ahead and tell me what kinda hairpin you're interested in.";
	}
	next;
	emotion e_heh;
	switch(select("^3131FFX Hairpin^000000:^3131FFBand Aid^000000:^3131FFFlower Hairpin^000000:No thanks.")) {
	case 1:
		if (countitem(2294) > 0 && countitem(7220) > 399) {
			mes "[Argen]";
			mes "X Hairpin!";
			mes "Nice choice~!";
			next;
			emotion e_gasp;
			mes "[Argen]";
			mes "Ah... Right.";
			mes "About the Stellar...";
			mes "^CE3100If you use an upgraded Stellar to make this item, any upgrades will be lost. That okay with you?^000000";
			next;
			switch(select("I don't mind.:I will come back later.")) {
			case 1:
				mes "[Argen]";
				mes "Alright, let's get to work.";
				next;
				mes "^3131FF * Scrape Scrape * ^000000";
				mes "^3131FF * Scrape Scrape * ^000000";
				next;
				emotion e_no1;
				mes "[Argen]";
				mes "Alright, I'm done!";
				mes "So what do you think?";
				mes "Pretty cool, eh?";
				delitem 2294,1; //Star_Dust
				delitem 7220,400; //Ectoplasm
				getitem 5079,1; //Crossed_Hair_Band
				next;
				break;
			case 2:
				mes "[Argen]";
				mes "No problem, take your time.";
				close;
			}
		}
		else {
			mes "[Argen]";
			mes "Oh~";
			mes "You want a ^3131FFX Hairpin?^000000";
			mes "That's easy!";
			next;
			mes "[Argen]";
			mes "I need ^FF00001 Stellar^000000";
			mes "and ^FF0000400 Ectoplasm^000000.";
			mes "Tell me when you're ready!";
			close;
		}
		break;
	case 2:
		if (countitem(970) > 0 && countitem(930) > 499) {
			mes "[Argen]";
			mes "Band Aid, eh?";
			mes "Got it! Hold on";
			mes "a sec, I'll be";
			mes "done in a bit.";
			next;
			mes "^3131FF * Scrape Scrape * ^000000";
			mes "^3131FF * Scrape Scrape * ^000000";
			next;
			emotion e_no1;
			mes "[Argen]";
			mes "*Whew~!*";
			mes "I'm finished!";
			mes "Pretty cool, huh?";
			delitem 970,1; //Alchol
			delitem 930,500; //Rotten_Bandage
			getitem 5063,1; //Plaster
			next;
		}
		else {
			mes "[Argen]";
			mes "Ah, a ^3131FFBand Aid^000000!";
			mes "That stuff is great for faking injuries, or hiding a bald spot. Handy to have when you wanna ditch school.";
			next;
			mes "[Argen]";
			mes "Bring me ^FF00001 Alcohol^000000 and";
			mes "^FF0000500 Rotten Bandage^000000!";
			mes "Tell me when you're ready, yeah?";
			close;
		}
		break;
	case 3:
		if (countitem(2269) > 0 && countitem(999) > 9 && Zeny > 19999) {
			mes "[Argen]";
			mes "You wanna a Flower Hairpin~?";
			mes "Alright, gimmie a minute, yeah?";
			next;
			mes "^3131FF - Scrape Scrape - ^000000";
			mes "^3131FF - Scrape Scrape - ^000000";
			next;
			emotion e_no1;
			mes "[Argen]";
			mes "Ah... It's done!";
			mes "Hey, that looks pretty smooth! Oh, and uh, you can keep this leftover Stem.";
			delitem 2269,1; //Centimental_Flower
			delitem 999,10; //Steel
			Zeny -= 20000;
			getitem 5061,1; //Flower_Hairpin
			getitem 905,1; //Stem
			next;
		}
		else {
			mes "[Argen]";
			mes "You wanna a ^3131FFFlower Hairpin^000000!";
			mes "Yeah, I've noticed cuties just love walking around with flowers in their mouths, and I was always like 'Whaaaat~?' But then I got this great idea!";
			next;
			mes "[Argen]";
			mes "Wouldn't some kinda... ^3131FFFlower Hairpin^000000 be a hit with chicks? So I tried making one, and it seemed to be pretty popular among my lady friends.";
			next;
			mes "[Argen]";
			mes "And it's a much better way to use a flower than to put it on Pet Orc Warriors. Man, those things just look gnarly~";
			next;
			mes "[Argen]";
			mes "Well, if you want one, I'll make it. But I want you should pay me some royalties for my services. It's pretty hard to attach a flower to a hairpin, you know?";
			next;
			mes "[Argen]";
			mes "It takes mad skill.";
			next;
			mes "[Argen]";
			mes "So, I want you to bring...";
			mes "^FF00001 Romantic Flower^000000,";
			mes "^FF000010 Steel^000000";
			mes "and ^3131FF20,000 zeny^000000!";
			mes "Got it? Cool~";
			close;
		}
		break;
	case 4:
		mes "[Argen]";
		mes "I'm down with that.";
		mes "If you want some kinda hairpin, just come back. I'll just be here, looking ^FF3333suaaave^000000 with this yo-yo.";
		close;
	}
	emotion e_paper;
	mes "[Argen]";
	mes "Feel free to come ask me if you want more hairpins. Hey, I know I look lazy, but I do like to make things for the ladies. See ya later~!";
	close;
}