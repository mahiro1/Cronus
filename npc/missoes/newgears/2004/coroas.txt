//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-  Coroas (Equipamentos para Cabeça 2004)                   -
//-------------------------------------------------------------
//- Por:                                                      -
//-  (*)Athena Dev Team                                       -
//-------------------------------------------------------------
//- Versão: 1.6c                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-   Missão para adquirir o equipamento Sinos Dourados,      -
//-   Coroa de Rainha Antigas e Coroa de Abelha Rainha.       -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//- 	1.6c - Realizado tradução do script. [?]              -
//-------------------------------------------------------------

yuno_in03,20,18,6	script	Nehris#1	4_F_JOB_BLACKSMITH,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again after -";
		mes "- you put some items into Kafra Storage. -";
		close;
	}
	mes "[Nehris]";
	mes "The Monster Museum has prepared a special event for your support~!";
	next;
	mes "[Nehris]";
	mes "Accessories typically worn by monsters are now available for adventurers to wear!";
	next;
	mes "[Nehris]";
	mes "Of course, since we're on a trial period, we can only provide 3 monster accessories to our fans. However, according to your response, we may add more monster accessories in the future.";
	next;
	mes "[Nehris]";
	mes "Simply bring the required materials, and pay a small fee, and we will provide you with the monsters accessory of your choice.";
	next;
	mes "[Nehris]";
	mes "Please choose the";
	mes "accessory you are";
	mes "interested in.";
	next;
	switch(select("Decorative Golden Bell:Crown of Ancient Queen:Crown of Mistress")) {
	case 1:
		mes "[Nehris]";
		mes "This Golden Bell is fashioned after the one worn by the 'Sohee' monster. It's a cute accessory I personally recommend for us ladies. A little oversized, but I believe you'll like it a lot.";
		next;
		if (countitem(10016) > 0 && countitem(714) > 0 && countitem(969) > 2 && Zeny > 19999) {
			mes "[Nehris]";
			mes "Thank you for";
			mes "bringing everything.";
			mes "Please wait one moment.";
			next;
			mes "^3355FF* Clang Clang! Scrape Scrape! Jingle Jingle! *^000000";
			next;
			delitem 10016,1; //Golden_Bell
			delitem 714,1; //Emperium
			delitem 969,3; //Gold
			Zeny -= 20000;
			getitem 5091,1; //Big_Golden_Bell
			mes "[Nehris]";
			mes "Yoohoo~";
			mes "There you go. Here's your beautiful Golden Bell. I doubt it will jingle if you shake your head, though.";
			next;
			mes "[Nehris]";
			mes "Oh, one more thing, don't confuse yourself with Sohee and commit suicide!";
			next;
			mes "[Nehris]";
			mes "If you'd like me to make another one, please come again. Thank you for visiting the Monster Museum.";
			close;
		}
		mes "[Nehris]";
		mes "The required materials for the Decorative Golden Bell are...";
		mes "^3355FF1 Golden Bell, 1 Emperium, 3 Gold^000000 and";
		mes "20,000 zeny.";
		next;
		mes "[Nehris]";
		mes "Just come ask me";
		mes "when you're ready.";
		mes "Thank you.";
		close;
	case 2:
		mes "[Nehris]";
		mes "This crown is inspired by the the one worn by the 'Isis' monster. It has a elegant look fitting for ancient royalty.";
		next;
		if (countitem(10006) > 0 && countitem(714) > 0 && countitem(969) > 2 && Zeny > 19999) {
			mes "[Nehris]";
			mes "Thank you for";
			mes "bringing everything.";
			mes "Please wait one moment.";
			next;
			mes "^3355FF* Clang Clang! Scrape Scrape! Jingle Jingle! *^000000";
			next;
			delitem 10006,1; //Queen's_Hair_Ornament
			delitem 714,1; //Emperium
			delitem 969,3; //Gold
			Zeny -= 20000;
			getitem 5080,1; //Headgear_Of_Queen
			mes "[Nehris]";
			mes "Tah dah!";
			mes "Here's your Crown of Ancient Queen! I don't know if you can get anyone to bow before you while wearing this, but at least you'll look elegant.";
			next;
			mes "[Nehris]";
			mes "If you'd like me to make another one, please come again. Thank you for visiting the Monster Museum.";
			close;
		}
		mes "[Nehris]";
		mes "The required materials for the Crown of Ancient Queen are...";
		mes "^3355FF1 Queen's Hair Ornament, 1 Emperium, 3 Gold^000000 and";
		mes "20,000 zeny.";
		next;
		mes "[Nehris]";
		mes "Just come ask me";
		mes "when you're ready.";
		mes "Thank you.";
		close;
	case 3:
		mes "[Nehris]";
		mes "This crown is an imitation of the one worn by the 'Mistress' monster. Wearing this will give you a look of brusque authority.";
		next;
		if (countitem(2249) > 0 && countitem(714) > 0 && countitem(969) > 2 && Zeny > 39999) {
			mes "[Nehris]";
			mes "Thank you for";
			mes "bringing everything.";
			next;
			mes "[Nehris]";
			mes "^FF0000By the way, if you use an upgraded";
			mes "Coronet for making this item,";
			mes "the item you create out of the Coronet";
			mes "will not have its upgraded status.^000000";
			next;
			mes "[Nehris]";
			mes "^FF0000Also, please only have one Coronet in your inventory to make the Crown of Mistress. We are not responsible for any loss.";
			next;
			mes "[Nehris]";
			mes "Are you sure you want to create the Crown of Mistress?";
			next;
			if (select("Yes:Wait a minute...!") == 1) {
				mes "[Nehris]";
				mes "Please wait one moment.";
				next;
				mes "^3355FF* Clang Clang! Scrape Scrape! Jingle Jingle! *^000000";
				next;
				delitem 2249,1; //Coronet
				delitem 714,1; //Emperium
				delitem 969,3; //Gold
				Zeny -= 40000;
				getitem 5081,1; //Mistress_Crown
				mes "[Nehris]";
				mes "Yay! You've got a great looking crown!";
				next;
				mes "[Nehris]";
				mes "I don't know if everyone will obey your commands while wearing this, but at least you can feel beautifully regal.";
				next;
				mes "[Nehris]";
				mes "Why don't you go take a picture with Mistress? That would be funny to see if she wasn't too busy trying to maul you.";
				next;
				mes "[Nehris]";
				mes "If you'd like me to make you another one, please come again. Thank you for visiting the Monster Museum.";
				close;
			}
			mes "[Nehris]";
			mes "No problem, just be sure to understand our precautions well, okay?";
			close;
		}
		mes "[Nehris]";
		mes "The required materials for the Crown of Mistress are...";
		mes "^3355FF1 Coronet, 1 Emperium, 3 Gold^000000 and";
		mes "40,000 zeny.";
		next;
		mes "[Nehris]";
		mes "Just come ask me";
		mes "when you're ready.";
		mes "Thank you.";
		close;
	}
}