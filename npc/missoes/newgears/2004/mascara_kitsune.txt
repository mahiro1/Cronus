//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-  Máscara Kitsune (Equipamentos para Cabeça 2004)          -
//-------------------------------------------------------------
//- Por:                                                      -
//-  (*)Athena Dev Team                                       -
//-------------------------------------------------------------
//- Versão: 1.6c                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-   Missão para adquirir o equipamento Máscara Kitsune.     -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//- 	1.6c - Realizado tradução do script. [?]              -
//-------------------------------------------------------------

pay_dun03,48,84,4	script	Nine Tails#Kitsune Mask	NINE_TAIL,1,1,{
	end;

OnInit:
	disablenpc "Nine Tails#Kitsune Mask";
	end;

OnTouch:
	mes "[Nine Tails]";
	mes "Yelp! Yelp yelp!";
	mes "Yelp! Yelp yelp!";
	next;
	if (select("Shake head:Nod head") == 1) {
		mes "[Nine Tails]";
		mes "Yelp! Yelp yelp!";
		close2;
		set getvariableofnpc(.MyMobs1,"SpawnManager#Kitsune"),1;
		switch(rand(1,25)) {
		case 1: set .@x,118;	set .@y,66;	break;
		case 2: set .@x,103;	set .@y,194;	break;
		case 3: set .@x,116;	set .@y,50;	break;
		case 4: set .@x,49;	set .@y,94;	break;
		case 5: set .@x,232;	set .@y,232;	break;
		case 6: set .@x,245;	set .@y,86;	break;
		case 7: set .@x,217;	set .@y,133;	break;
		case 8: set .@x,50;	set .@y,157;	break;
		case 9: set .@x,245;	set .@y,60;	break;
		case 10: set .@x,220;	set .@y,77;	break;
		case 11: set .@x,198;	set .@y,62;	break;
		case 12: set .@x,158;	set .@y,41;	break;
		case 13: set .@x,57;	set .@y,210;	break;
		case 14: set .@x,251;	set .@y,207;	break;
		case 15: set .@x,86;	set .@y,130;	break;
		case 16: set .@x,216;	set .@y,233;	break;
		case 17: set .@x,192;	set .@y,245;	break;
		case 18: set .@x,117;	set .@y,234;	break;
		case 19: set .@x,144;	set .@y,255;	break;
		case 20: set .@x,190;	set .@y,216;	break;
		case 21: set .@x,63;	set .@y,66;	break;
		case 22: set .@x,247;	set .@y,183;	break;
		case 23: set .@x,37;	set .@y,225;	break;
		case 24: set .@x,247;	set .@y,118;	break;
		case 25: set .@x,154;	set .@y,130;	break;
		}
		monster "pay_dun03",.@x,.@y,"Nine Tail",1180,1,"SpawnManager#Kitsune::OnMyMobDead";
		specialeffect EF_FLASHER;
		disablenpc "Nine Tails#Kitsune Mask";
		end;
	}
	close2;
	specialeffect EF_FLASHER;
	disablenpc "Nine Tails#Kitsune Mask";
	enablenpc "Nine Tails#Kitsune Man";
	end;
}

pay_dun03,48,84,4	script	Nine Tails#Kitsune Man	4_M_JPN2,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "- Wait a moment! -";
		mes "- Currently you are carrying -";
		mes "- too many items with you. -";
		mes "- Please come back after -";
		mes "- you put some items into Kafra Storage. -";
		close;
	}
	if (countitem(1022) > 998) {
		mes "[Nine Tails]";
		mes "Oh! You finally brought what I've asked, human. Okay, now I can bring her back to life!";
		next;
		if (select("Give items:Cancel") == 1) {
			delitem 1022,999; //Fox_Tail
			getitem 5069,1; //Mask_Of_Fox
			mes "[Nine Tails]";
			mes "Now...";
			mes "Take this";
			mes "and get out";
			mes "of my sight~!";
			close;
		}
		close2;
	}
	else {
		mes "[Nine Tails]";
		mes "......Errr?";
		mes "Now why am I in human form again?";
		next;
		mes "[Nine Tails]";
		mes "I guess... I need to practice transformation more often. *Sigh*";
		next;
		mes "[Nine Tails]";
		mes "Anyway, I've got a bone to pick with you humans. You're an aggressive and greedy race, always killing things and taking stuff. And despite all the hunting, you never seem to be satisfied.";
		next;
		mes "[Nine Tails]";
		mes "You always intrude our territory to slay us foxes for our nice, soft fur. But it's not only us you harass. The Sohees always complain that you barbaric humans keep stealing their clothing!";
		next;
		mes "[Nine Tails]";
		mes "My wife was killed as a victim of human greed, probably skinned for her fabulous pelt...";
		next;
		mes "[Nine Tails]";
		mes "All I want is to bring her back to life, no matter what the cost. That's why I'm swallowing my pride and asking you for help.";
		next;
		mes "[Nine Tails]";
		mes "I need...";
		mes "^0000FF999^000000 ^FF0000Nine Tail^000000";
		mes "in order to cast the Fox Resurrection spell.";
		next;
		mes "[Nine Tails]";
		mes "I know how you humans think...";
		mes "You want some sort of reward for whatever mayhem you cause. Alright then...";
		next;
		mes "[Nine Tails]";
		mes "I will give you this ^0000FFKitsune Mask^000000 if you bring me what I asked. How does that sound? Remember, ^0000FF999 ^FF0000Nine Tail^000000.";
		close;
	}
	set getvariableofnpc(.MyMobs2,"SpawnManager#Kitsune"),1;
	switch(rand(1,25)) {
	case 1: set .@x,118;	set .@y,66;	break;
	case 2: set .@x,103;	set .@y,194;	break;
	case 3: set .@x,116;	set .@y,50;	break;
	case 4: set .@x,49;	set .@y,94;	break;
	case 5: set .@x,232;	set .@y,232;	break;
	case 6: set .@x,245;	set .@y,86;	break;
	case 7: set .@x,217;	set .@y,133;	break;
	case 8: set .@x,50;	set .@y,157;	break;
	case 9: set .@x,245;	set .@y,60;	break;
	case 10: set .@x,220;	set .@y,77;	break;
	case 11: set .@x,198;	set .@y,62;	break;
	case 12: set .@x,158;	set .@y,41;	break;
	case 13: set .@x,57;	set .@y,210;	break;
	case 14: set .@x,251;	set .@y,207;	break;
	case 15: set .@x,86;	set .@y,130;	break;
	case 16: set .@x,216;	set .@y,233;	break;
	case 17: set .@x,192;	set .@y,245;	break;
	case 18: set .@x,117;	set .@y,234;	break;
	case 19: set .@x,144;	set .@y,255;	break;
	case 20: set .@x,190;	set .@y,216;	break;
	case 21: set .@x,63;	set .@y,66;	break;
	case 22: set .@x,247;	set .@y,183;	break;
	case 23: set .@x,37;	set .@y,225;	break;
	case 24: set .@x,247;	set .@y,118;	break;
	case 25: set .@x,154;	set .@y,130;	break;
	}
	monster "pay_dun03",.@x,.@y,"Nine Tail",1180,1,"SpawnManager#Kitsune::OnMyMobDead2";
	specialeffect EF_BLASTMINEBOMB;
	disablenpc "Nine Tails#Kitsune Man";
	end;

OnInit:
	set getvariableofnpc(.MyMobs2,"SpawnManager#Kitsune"),1;
	monster "pay_dun03",48,83,"Nine Tail",1180,1,"SpawnManager#Kitsune::OnMyMobDead2";
	disablenpc "Nine Tails#Kitsune Man";
	end;
}

pay_dun03,48,84,4	script	SpawnManager#Kitsune	-1,{
OnInit:
	set .MyMobs1,1;
	set .MyMobs2,1;
	end;

OnMyMobDead:
	set .MyMobs1,.MyMobs1-1;
	if (.MyMobs1 < 1)
		enablenpc "Nine Tails#Kitsune Man";
	end;

OnMyMobDead2:
	set .MyMobs2,.MyMobs2-1;
	if (.MyMobs2 < 1)
		enablenpc "Nine Tails#Kitsune Mask";
	end;

}