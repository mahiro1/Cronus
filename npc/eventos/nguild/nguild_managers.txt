//===== Cronus Script ======================================
//= Gerentes Evento WoE Baby
//===== By: ==================================================
//= kobra_k88 // Mihael
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//=
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, etc.
//===== Additional Comments: =================================
//= 1.0 Based off existing guild scripts.  Do not know if it is accurate.[kobra_k88]
//= 1.1 Added modified guild manager function. [L0ne_W0lf]
//= 	Removed guardian summoning.
//= 1.2 Corrected function name. (bugreport:1728) [L0ne_W0lf]
//= 1.3 Tradução para PT-BR Adicionada. [Mihael]
//============================================================

//==============================================
function	script	F_GldManager	{

	set @GID, getcastledata(getarg(1),1);
	mes "[ "+getarg(0)+" ]";
	if (@GID == 0){
		mes "Estou esperando por meu mestre. Bravo aventureiro, siga seu destino!";
		return;
	}
	if (getcharid(2) != @GID){
		mes "Estou aqui para seguir o mestre ^5533FF" + getguildmaster(@GID) + "^000000! Opa! Você não faz parte da guild dele!!";
		mes "Onde estão os guardiões? Destruam esses intrusos!";
		return;
	}
	if (strcharinfo(0) != getguildmaster(@GID)){
		mes "Você não é o ^5533FF" + getguildmaster(@GID) + "^000000! Estou aqui apenas para seguir ^5533FF" + getguildmaster(@GID) + "^000000 meu Comandante.";
		return 0;
	}

	mes "Bem-vindo Mestre ^5533FF" + getguildmaster(@GID) + "^000000 ! Eu vou ajudá-lo de qualquer maneira que puder!";
	next;

	menu "Contratar/Demitir Serviços Kafra",M_Kaf, "Entrar na Sala do Tesouro",M_Treas, "Cancelar",M_End;

	//===========================
	M_Kaf:
	//======
		mes "[ "+getarg(0)+" ]";
		if (getcastledata(getarg(1),9) == 1) goto L_Dismiss;
		if (getgdskilllv(@GID,10001) == 0){
			mes "Mestre, você não tem um contrato com os serviços Kafra.";
			mes "Antes de contratar uma Kafra, você deve primeiro aprender a habilidade de Guild ^5533FFContrato com Kafra^000000.";
			return;
		}

		L_Hire:
			mes "Você gostaria de contratar os serviços de uma Kafra? Você vai precisar de ^5533FF10,000 Zenys^000000 para isso... ";
			next;
			menu "Contratar Kafra.",-,"Cancelar",sM_KafEnd;

				mes "[ "+getarg(0)+" ]";
				if (Zeny < 10000){
					mes "Mestre, você não tem dinheiro suficiente para contratar uma Kafra. O contrato foi cancelado.";
					return;
				}
				Zeny -= 10000;
				enablenpc "Funcionária Kafra#"+getarg(4);
				setcastledata getarg(1),9,1;
				mes "Você criou um contrato com a empresa Kafra.";
				next;
				cutin "kafra_01",2;
				mes "[ Funcionária Kafra ]";
				mes "Como vai? Estou aqui para lhe fornecer um serviço útil! Eu vou fazer o melhor que posso para atendê-lo.";
				next;
				cutin "kafra_01",255;
				mes "[ "+getarg(0)+" ]";
				//mes "O seu contrato de trabalho dura ^5533FF1 mês^000000.  Após este longo prazo, você terá que criar um novo contrato.";
				mes "Eu acho que a Funcionária Kafra irá beneficiar nossos membros da guilda.";
				return;

			sM_KafEnd:
				mes "[ "+getarg(0)+" ]";
				mes "Como desejar Mestre. Mas eu sugiro que nós contratemos uma Kafra o mais rápido possível!";
				return;

		L_Dismiss:
			mes "Gostaria de demitir a Kafra atual?";
			next;
			menu "Demitir",-,"Cancelar",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[ Funcionária Kafra ]";
				mes "Fiz alguma coisa errada? Se eu fiz, por favor, você pode me perdoar?";
				next;
				menu "Demitir",-,"Cancelar",ssM_KafEnd2;

					mes "[ Funcionária Kafra ]";
					mes "É uma pena que possa ser capaz de servir a sua Guild....";
					next;
					disablenpc "Funcionária Kafra#"+getarg(4);
					setcastledata getarg(1),9,0;
					cutin "kafra_01",255;
					mes "[ "+getarg(0)+" ]";
					mes "A Kafra foi demitida. Mas ... nós deveríamos realmente ter uma Kafra o mais rápido possível!";
					return;
				ssM_KafEnd2:
					mes "[ Funcionária Kafra ]";
					mes "Obrigada mestre, eu vou fazer o meu melhor! ^^.";
					cutin "kafra_01",255;
					return;
			sM_KafEnd2:
				mes "[ "+getarg(0)+" ]";
				mes "Mestre, eu acho que você deve manter a Funcionária Kafra atual porque ela já está dando o seu melhor para nos servir";
				return;

	//=========================
	M_Treas:
	//========
		mes "[ "+getarg(0)+" ]";
		mes "Quer ir para a nossa Sala do Tesouro? Só você, o Guild Master, está autorizado a entrar nesta sala.";
		next;
		menu "Entrar na Sala do Tesouro.",-,"Cancelar",sM_TresEnd;

			mes "[ "+getarg(0)+" ]";
			mes "Por favor, siga-me pela passagem secreta.";
			mes "Você deve puxar para baixo a alavanca secreta quando quiser sair.";
			next;
			warp getarg(1),getarg(2),getarg(3);
			return;
		sM_TresEnd:
			mes "[ "+getarg(0)+" ]";
			mes "As mercadorias são produzidas todos os dias.";
			mes "Você deve levá-las sempre que puder, pois elas podem desaparecer se você levá-las na hora errada.";
			return;


	//==========================
	M_End:
	//=======
		mes "[ "+getarg(0)+" ]";
		mes "Como quiser, senhor.";
		return;
}

// Castle 1 ==================================================================================
nguild_alde,218,175,0	script	Frolo	1_M_JOBTESTER,{
	callfunc "F_GldManager","Frolo","nguild_alde",119,223,"N01";
	close;
}


// Castle 2 ==================================================================================
nguild_gef,40,48,5	script	Leiber	1_M_JOBTESTER,{
	callfunc "F_GldManager","Leiber","nguild_gef",155,112,"N02";
	close;
}


// Castle 3 ==================================================================================
nguild_pay,120,58,4	script	Dundar	1_M_JOBTESTER,{
	callfunc "F_GldManager","Dundar","nguild_pay",290,7,"N03";
	close;
}


// Castle 4 ==================================================================================
nguild_prt,112,181,0	script	Thefton	1_M_JOBTESTER,{
	callfunc "F_GldManager","Thefton","nguild_prt",15,209,"N04";
	close;
}
