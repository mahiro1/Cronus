//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
// Estilista
//----- Por: -------------------------------------------------
//= Euphy
//----- Versão Atual: ----------------------------------------
//= 1.2
//----- Descrição: -------------------------------------------
//= Você pode alterar seu estilo e cor do cabelo, e as cores
// e as cores de sua roupa.
//----- Comentários Adicionais: ------------------------------
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//= 1.2 Script traduzido. [Cainho]
//------------------------------------------------------------

prontera,170,180,1	script	Estilista#estilista_personalizado	2_M_DYEINGER,{

	setarray .@styles[1],getbattleflag("max_cloth_color"),getbattleflag("max_hair_style"),getbattleflag("max_hair_color");
	setarray .@Look[1],7,1,6;
	set .@s, select(" ~ Cor da Roupa: ~ Estilo do Cabelo: ~ Cor do Cabelo");
	set .@Revert, getlook(.@Look[.@s]);
	set .@style,1;
	while(1) {
		setlook .@Look[.@s], .@style;
		message strcharinfo(0),"Este é o estilo #"+.@style+".";
		set .@menu$, " ~ Próximo (^0055FF"+((.@style!=.@styles[.@s])?.@style+1:1)+"^000000): ~ Anterior (^0055FF"+((.@style!=1)?.@style-1:.@styles[.@s])+"^000000): ~ Informarei o estilo: ~ Voltar ao Original (^0055FF"+.@Revert+"^000000)";
		switch(select(.@menu$)) {
			case 1: set .@style, ((.@style!=.@styles[.@s])?.@style+1:1); break;
			case 2: set .@style, ((.@style!=1)?.@style-1:.@styles[.@s]); break;
			case 3: message strcharinfo(0),"Choose a style between 1 - "+.@styles[.@s]+".";
				input .@style,0,.@styles[.@s];
				if (!.@style) set .@style, rand(1,.@styles[.@s]);
				break;
			case 4: set .@style, .@Revert; setlook .@Look[.@s], .@Revert; break;
		}
	}
}
