//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-           Guerra do Emperium (Geffen)                     -
//-------------------------------------------------------------
//- Por:                                                      -
//-  L0ne_W0lf e Masao.                                       -
//-------------------------------------------------------------
//- Versão: 1.2b                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-  Npcs relacionados ao 5º Castelo de Geffen.               -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//- 	1.2b - Relizado tradução do script. [SoulBlaker]      -
//-------------------------------------------------------------

// Bandeiras do Clã (Fora do Castelo)
//-------------------------------------------------------------
-	script	Arima#1::OutsideFlagsG5	GUILD_FLAG,{

	set .@GID, getcastledata("gefg_cas05",1);

	if (.@GID == 0) {
		mes "[ Decreto do Reino Divino de Rune-Midgard ]";
		mes " ";
		mes "1. Siga a ordenança do Divino Reino de Rune-Midgad, ";
		mes "declaramos que";
		mes "que não há nenhum mestre formal deste castelo.";
		mes " ";
		mes "2. Para o único quem puder ";
		mes "superar todas provações";
		mes "e destruír o Emperium,";
		mes "o rei vai dotar aquele como";
		mes "proprietário do castelo.";
		close;
	}
	if (getcharid(2) == .@GID) {
		mes "[ Voz Sussurrando ]";
		mes "Aos bravos...";
		mes "Deseja retornar ao seu lugar de honra?";
		next;
		if(select("Retornar ao Castelo do Clã.:Desistir.") == 1) {
			close2;
			if (getcharid(2) == getcastledata("gefg_cas05",1)) {
				warp "gefg_cas05",168,31;
				end;
			}
			end;
		}
		close;
	}
	mes "[ Decreto do Reino Divino de Rune-Midgard ]";
	mes " ";
	mes "1. Siga a ordenança do Divino Reino de Rune-Midgard ";
	mes "que este lugar é de";
	mes "posse privada do Clã ^ff0000"+getguildname(.@GID)+"^000000.";
	mes " ";
	mes "2. O Mestre do Clã ^ff0000"+getguildname(.@GID)+"^000000 é";
	mes "^ff0000"+getguildmaster(.@GID)+"^000000";
	mes "Se há alguém que se opõe a isso, ";
	mes "que prove sua força e honra com uma lâmina de aço em sua mão.";
	close;

OnRecvCastleG05:
	flagemblem getcastledata("gefg_cas05",1);
	end;
}

gef_fild13,302,87,7	duplicate(OutsideFlagsG5)	Arima#1-2	GUILD_FLAG
gef_fild13,313,83,0	duplicate(OutsideFlagsG5)	Arima#2	GUILD_FLAG
gef_fild13,252,51,2	duplicate(OutsideFlagsG5)	Arima#3	GUILD_FLAG
gef_fild13,26,147,2	duplicate(OutsideFlagsG5)	Arima#4	GUILD_FLAG

// Bandeiras do Clã (Dentro do Castelo)
//-------------------------------------------------------------
-	script	Arima#5::InsideFlagsG5	GUILD_FLAG,{
	end;

OnRecvCastleG05:
	flagemblem getcastledata("gefg_cas05",1);
	end;
}

gefg_cas05,77,185,7	duplicate(InsideFlagsG5)	Arima#5-2	GUILD_FLAG
gefg_cas05,92,181,0	duplicate(InsideFlagsG5)	Arima#6	GUILD_FLAG
gefg_cas05,83,158,1	duplicate(InsideFlagsG5)	Arima#7	GUILD_FLAG
gefg_cas05,62,144,7	duplicate(InsideFlagsG5)	Arima#8	GUILD_FLAG
gefg_cas05,62,66,4	duplicate(InsideFlagsG5)	Arima#9	GUILD_FLAG
gefg_cas05,69,66,4	duplicate(InsideFlagsG5)	Arima#10	GUILD_FLAG

// Bandeiras do Clã (Cidade)
//-------------------------------------------------------------
geffen,131,123,6	duplicate(InsideFlagsG5)	Arima#11	GUILD_FLAG

// Funcionária Kafra
//-------------------------------------------------------------
gefg_cas05,61,52,5	duplicate(guildkafra)	Funcionária Kafra#gefg_cas05	4_F_KAFRA1

// Gerente
//-------------------------------------------------------------
gefg_cas05,70,52,3	duplicate(cm)	Byohre#gefg_cas05	1_M_JOBTESTER

// Saída da Sala do Tesouro
//-------------------------------------------------------------
gefg_cas05,149,107,0	script	#lever_G05	HIDDEN_NPC,{

	mes " ";
	mes "Há uma pequena alavanca. Deseja acioná-la?";
	next;
	if(select("Puxar.:Não Puxar.") == 1) {
		close2;
		warp "gefg_cas05",70,53;
		end;
	}
	close;
}

// Alavanca do Calabouço do Clã
//-------------------------------------------------------------
gefg_cas05,65,22,0	duplicate(gdlever)	#gefg_cas05	HIDDEN_NPC

// Baús do Tesouro do Clã
//-------------------------------------------------------------
gefg_cas05,144,110,0	duplicate(Gld_Trea_Spawn)	Treasure#gefg_cas05	-1

// Gerente de Eventos da Guerrra
//-------------------------------------------------------------
gefg_cas05,194,184,0	duplicate(Gld_Agit_Manager)	Agit#gefg_cas05	-1

// Guardiões
//-------------------------------------------------------------
gefg_cas05,70,52,3	duplicate(Gld_Guard_Template)	Guardian#gefg_cas05	-1