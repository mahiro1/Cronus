//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-        Guerra do Emperium - Ligação Automática            -
//-------------------------------------------------------------
//- Por:                                                      -
//-  kalen (1.0), (1.1) Akaru e ho|yAnge|, (1.1a~1.3a)        -
//-  kobra_k88, (1.4) Lupus, (1.5) KarLaeda, 1.6 ultramage e  -
//-  (1.7~2.0a) L0ne_W0lf.                                    -
//-------------------------------------------------------------
//- Versão: 1.4b                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-	 Liga Automáticamente a Guerra do Emperium.           -
//-                                                           -
//-  gettime(3): Obtem horas (tempo de 24 horas)              -
//-  gettime(4): Obtem dias da semana 1 = Segunda, 2 = Terça  -
//-              3 = Quarta, 4 = Quinta, etc.                 -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//- 	1.4b - 2.0a Removed some brackets [L0ne_W0lf]         -
//-------------------------------------------------------------

// Horário pra Ligar/Desligar a Guerrra do Emperium
//-------------------------------------------------------------
-	script	Agit_Event	-1,{
	end;

OnClock2100:	// horário de iniciar Terça(2), Quinta(4)
OnClock2300:	// horário de encerrar Terça(2), Quinta(4)
OnClock1600:	// horário de iniciar Sabado(6)
OnClock1800:	// horário de encerrar Sabado(6)

OnAgitInit:
	// verificação de tempo de iniciar
	if((gettime(4)==2) && (gettime(3)>=21 && gettime(3)<23) ||
	   (gettime(4)==4) && (gettime(3)>=21 && gettime(3)<23) ||
	   (gettime(4)==6) && (gettime(3)>=16 && gettime(3)<18)) {
		if (!agitcheck()) {
			agitstart;
			callsub S_DisplayOwners;
		}
		end;
	}

	// verificação de tempo de encerramento
	if ((gettime(4)==2) && (gettime(3)==23) ||
	    (gettime(4)==4) && (gettime(3)==23) ||
	    (gettime(4)==6) && (gettime(3)==18)) {
		if (agitcheck()) {
			agitend;
			callsub S_DisplayOwners;
		}
		end;
	}
	end;

S_DisplayOwners:
	setarray .@maps$[0],"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05";
	setarray .@maps$[5],"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05";
	setarray .@maps$[10],"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05";
	setarray .@maps$[15],"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05";
	for( set .@i, 0; .@i <= 19; set .@i, .@i+1 ) {
		if (getcastledata(.@maps$[.@i],1))
			announce "O castelo de [" + getcastlename(.@maps$[.@i]) + "] foi conquistado pelo Clã [" + getguildname(getcastledata(.@maps$[.@i],1)) + "].",bc_all|bc_woe;
		else
			announce "O castelo de [" + getcastlename(.@maps$[.@i]) + "] está atualmente desocupado.",bc_all|bc_woe;
	}
	end;
}